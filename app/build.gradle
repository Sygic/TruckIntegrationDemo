apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    defaultConfig {
        applicationId "com.sygic.integdemo3d"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        release {
            Properties localProp = new Properties()
            localProp.load(project.rootProject.file('local.properties').newDataInputStream())
            def keystoreSett = localProp.getProperty('keystore.settings', null)
            if (keystoreSett != null) {
                File signFile = file(keystoreSett);
                if (signFile.exists()) {
                    Properties properties = new Properties()
                    properties.load(signFile.newDataInputStream())

                    storeFile file(properties.getProperty('key.store', null))
                    storePassword properties.getProperty('key.store.password', null)
                    keyAlias properties.getProperty('key.alias', null)
                    keyPassword properties.getProperty('key.alias.password', null)
                }
                else {
                    logger.warn("File " + signFile + " does not exist. App will not be signed.")
                }
            }
            else {
                logger.warn("keystore.settings file not defined in local.properties. App will not be signed.")
            }
        } // release
    } // signingConfigs

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile project(':SygicLib')
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.android.support:design:25.0.1'
    compile 'com.google.android.gms:play-services-location:8.1.0'
}
